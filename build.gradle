plugins {
    id 'application' // Provide convenience executables for trying out the examples.
    id 'com.google.protobuf' version '0.9.4'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' // 确保版本是最新的
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.61.0' // CURRENT_GRPC_VERSION
def protocVersion = '3.25.1'

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-xds:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

startScripts.enabled = false

task xdsHelloWorldClient(type: CreateStartScripts) {
    mainClass = 'io.grpc.examples.helloworldxds.XdsHelloWorldClient'
    applicationName = 'xds-hello-world-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task xdsHelloWorldServer(type: CreateStartScripts) {
    mainClass = 'io.grpc.examples.helloworldxds.XdsHelloWorldServer'
    applicationName = 'xds-hello-world-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

application {
    applicationDistribution.into('bin') {
        from(xdsHelloWorldClient)
        from(xdsHelloWorldServer)
        fileMode = 0755
    }
    mainClass = 'io.grpc.examples.helloworldxds.XdsHelloWorldClient'
}


jar {
    // 设置生成的 JAR 文件的入口点
    manifest {
        attributes 'Main-Class': 'io.grpc.examples.helloworldxds.XdsHelloWorldClient'
    }

}

shadowJar {
    archiveBaseName.set('myapp')          // 设置基础名称
    archiveClassifier.set('')             // 避免在名称中添加 'all' 或其他分类器
    archiveVersion.set('1.0.0')            // 设置版本号

    // 如果需要，可以添加合并策略
    mergeServiceFiles {
        include 'META-INF/services/*'
    }
}

